import groovy.lang.Closure;

buildscript {
    repositories
    {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

def isAndroidProject(project) {
    project.hasProperty("androidProject") && project.property("androidProject") == 'true'
}

def isAndroidLibraryProject(project) {
    project.hasProperty("androidLibraryProject") && project.property("androidLibraryProject") == 'true'
}

def onReleaseVariant(project, Closure c) {
    if (isAndroidLibraryProject(project)) {
        project.android.libraryVariants.all {variant ->
            if (variant.buildType.name.equals(com.android.builder.core.BuilderConstants.RELEASE)) {
                c(variant)
            }
        }
    } else {
        project.android.applicationVariants.all {variant ->
            if (variant.buildType.name.equals(com.android.builder.core.BuilderConstants.RELEASE)) {
                c(variant)
            }
        }
    }
}

/**
 * Calls given closure with the output object of the given project.
 * This ensures that the output is already configured when the callback is called.
 * @param project
 * @param c callback
 * @return void
 */
def onReleaseOutput(project, Closure c) {
//    onReleaseVariant(project) {println it}
    onReleaseVariant(project) { variant ->
//        variant.outputs.each { output ->
//            c(output)
//        }
        def output = variant.outputs.find({ o -> o.name.contains(com.android.builder.core.BuilderConstants.RELEASE)})
        c(output)
    }
}

def getReleaseVariant(project) {
    if (isAndroidLibraryProject(project)) {
        project.android.libraryVariants.all {variant ->
//            println variant
            if (variant.buildType.name.equals(com.android.builder.core.BuilderConstants.RELEASE)) {
                return variant
            }
        }
    } else {
        project.android.applicationVariants.all {variant ->
//            println variant
            if (variant.buildType.name.equals(com.android.builder.core.BuilderConstants.RELEASE)) {
                return variant
            }
        }
    }
}

def getVariants(project) {
	if (isAndroidLibraryProject(project)) {
		return project.android.libraryVariants
	} else {
		project.android.applicationVariants
	}
}

def getSourceDir(project) {
    if (isAndroidProject(project)) {
        project.android.sourceSets.main.java.srcDirs
    } else {
        project.sourceSets.main.allJava
    }
}

// only return java sources 
// (getSourceDir includes xml, bpmn, aidl, ... which is wrong for javadoc generation)
def getOnlyJavaSources(project) {
    if (isAndroidProject(project)) {
    	getReleaseVariant(project).javaCompile.source
    } else {
        project.sourceSets.main.allJava
    }
}

def getResourcesDir(project) {
    if (isAndroidProject(project)) {
        project.android.sourceSets.main.res.srcDirs
    } else {
        project.sourceSets.main.resources
    }
}

def getCompileClassPath(project) {
    if (isAndroidProject(project)) {
        if (isAndroidLibraryProject(project)) {
            project.android.libraryVariants.javaCompile.classpath
        } else {
            project.android.applicationVariants.javaCompile.classpath
        }
    } else {
        project.sourceSets.main.compileClasspath
    }
}

def isExcluded(excl, name)
{
    boolean ret = false;
    if(excl!=null)
    {
        for(ex in excl)
        {
            if(name.indexOf(ex)!=-1)
            {
                ret = true;
                break;
            }
        }
    }
    return ret;
}

def calledWithTask(fulltaskname)
{
    def result = gradle.startParameter.taskNames.any {taskName -> taskName.contains(fulltaskname)}
    return result
}

def androidSdkExists() {
    def androidHome = System.env.'ANDROID_HOME'
    if (androidHome != null && !androidHome.isEmpty()) {
        def adb = new File(androidHome + File.separator + "platform-tools")
        return adb.exists() && adb.isDirectory();
    }
    return false;
}

// make functions externally visible

ext {
    isAndroidProject = this.&isAndroidProject
    isAndroidLibraryProject = this.&isAndroidLibraryProject
    getReleaseVariant = this.&getReleaseVariant
    getVariants = this.&getVariants
    getSourceDir = this.&getSourceDir
    getOnlyJavaSources = this.&getOnlyJavaSources
    getResourcesDir = this.&getResourcesDir
    getCompileClassPath = this.&getCompileClassPath
    isExcluded = this.&isExcluded
    calledWithTask = this.&calledWithTask
    androidSdkExists = this.&androidSdkExists
    onReleaseVariant = this.&onReleaseVariant
    onReleaseOutput = this.&onReleaseOutput
}